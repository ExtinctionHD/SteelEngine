#version 460
#extension GL_GOOGLE_include_directive : require

#define SHADER_STAGE compute
#pragma shader_stage(compute)

#include "Common/Common.glsl"
#include "Common/MonteCarlo.glsl"
#include "Common/PBR.glsl"
#include "Compute/Compute.glsl"

#define SAMPLE_COUNT 2048

layout(constant_id = 0) const uint LOCAL_SIZE_X = 16;
layout(constant_id = 1) const uint LOCAL_SIZE_Y = 16;
layout(constant_id = 2) const uint LOCAL_SIZE_Z = 1;

layout(
    local_size_x_id = 0,
    local_size_y_id = 1,
    local_size_z_id = 2) in;

layout(push_constant) uniform PushConstants{
    float roughness;
    uint faceIndex;
};

layout(set = 0, binding = 0) uniform samplerCube environmentMap;
layout(set = 1, binding = 0, rgba32f) uniform writeonly image2D reflectionFace;

void main()
{
    const uvec2 imageSize = imageSize(reflectionFace);

    const vec2 uv = GetUV(imageSize);
    
    const vec3 N = GetCubeDirection(faceIndex, uv);
    const vec3 R = N;
    const vec3 V = R;

    const mat3 TBN = GetTBN(N);

    const float a = roughness * roughness;
    const float a2 = a * a;
    
    vec3 result = vec3(0.0);
    float totalWeight = 0.0;
    for (uint i = 0; i < SAMPLE_COUNT; ++i)
    {
        const vec2 Xi = Hammersley(i, SAMPLE_COUNT);

        const vec3 H = TangentToWorld(ImportanceSampleGGX(Xi, a2), TBN);

        const vec3 L = -reflect(V, H);

        const float NoL = CosThetaWorld(N, L);
        const float NoH = CosThetaWorld(N, H);
        const float VoH = max(dot(V, H), 0.0);

        if (NoL > 0.0)
        {
            const float pdf = SpecularPdf(NoH, a2, VoH); 

            const float saTexel  = 4.0 * PI / (6.0 * imageSize.x * imageSize.x);
            const float saSample = 1.0 / (float(SAMPLE_COUNT) * pdf + EPSILON);

            const float lod = roughness == 0.0 ? 0.0 : 0.5 * log2(saSample / saTexel); 

            result += textureLod(environmentMap, L, lod).rgb * NoL;
            totalWeight += NoL;
        }
    }

    result /= totalWeight;

    imageStore(reflectionFace, ivec2(gl_GlobalInvocationID), vec4(result, 1.0));
}