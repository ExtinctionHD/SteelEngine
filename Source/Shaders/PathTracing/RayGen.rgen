#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_ray_query : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_nonuniform_qualifier : require

#define SHADER_STAGE raygen
#pragma shader_stage(raygen)

#include "Common/Common.h"
#include "Common/Common.glsl"
#include "Common/Debug.glsl"
#include "Common/Random.glsl"
#include "Common/RayTracing.glsl"
#include "PathTracing/PathTracing.glsl"

#define ACCUMULATION 1
#define RENDER_TO_HDR 0
#define RENDER_TO_CUBE 0

#define LIGHT_COUNT 0

#define MIN_BOUNCE_COUNT 2
#define MAX_BOUNCE_COUNT 4

#define MIN_THRESHOLD 0.05
#define MAX_IRRADIANCE 8.0

#define DIRECT_LIGHT_DISK_RADIUS 0.0047

layout(constant_id = 0) const uint SAMPLE_COUNT = 1;
layout(constant_id = 1) const uint MATERIAL_COUNT = 256;
layout(constant_id = 2) const float POINT_LIGHT_RADIUS = 0.05;

#if ACCUMULATION
layout(push_constant) uniform PushConstants{
    uint accumIndex;
};
#endif

#if RENDER_TO_HDR
layout(set = 0, binding = 0, rgba16f) uniform writeonly image2D renderTarget;
#else
layout(set = 0, binding = 0, rgba8) uniform writeonly image2D renderTarget;
#endif
#if ACCUMULATION
layout(set = 0, binding = 1, rgba32f) uniform image2D accumulationTarget;
#endif

layout(set = 1, binding = 0) uniform cameraBuffer{ CameraPT camera; };

#if LIGHT_COUNT
layout(set = 2, binding = 0) uniform lightBuffer{ Light lights[LIGHT_COUNT]; };
#endif
layout(set = 2, binding = 1) uniform samplerCube environmentMap;

layout(set = 2, binding = 2) uniform accelerationStructureEXT tlas;
layout(set = 2, binding = 3) uniform materialsBuffer{ Material materials[MATERIAL_COUNT]; };
layout(set = 2, binding = 4) uniform sampler2D textures[];

layout(set = 2, binding = 5) readonly buffer IndicesData{ uint indices[]; } indicesData[];
layout(set = 2, binding = 6) readonly buffer VerticesData{ VertexRT vertices[]; } verticesData[];

layout(location = 0) rayPayloadEXT MaterialPayload payload;

uvec2 GetSeed(uvec2 id, uint sampleIndex)
{
    const uint s0 = (id.x << 16) | id.y;
#if ACCUMULATION
    const uint s1 = (sampleIndex << 8) | accumIndex;
#else
    const uint s1 = sampleIndex << 8;
#endif

    uvec2 seed = uvec2(GetHash(s0), GetHash(s1));
    Rand(seed);

    return seed;
}

vec3 GetPrimaryRayOrigin()
{
    return camera.inverseView[3].xyz;
}

vec3 GetPrimaryRayDireciton(uvec2 seed)
{
    const vec2 pixelSize = 1.0 / gl_LaunchSizeEXT.xy;
    const vec2 uv = pixelSize * gl_LaunchIDEXT.xy + pixelSize * NextVec2(seed);
#if RENDER_TO_CUBE
    const vec2 xy = (uv * 2.0 - 1.0) * vec2(-1.0, 1.0);
#else
    const vec2 xy = uv * 2.0 - 1.0;
#endif
    const vec4 target = camera.inverseProj * vec4(xy, 1.0, 1.0);
    const vec4 direction = camera.inverseView * vec4(normalize(target.xyz), 0.0);

    return normalize(direction.xyz);
}

void UnpackMaterial(Material mat, inout Surface surface)
{
    surface.TBN = GetTBN(payload.normal);
    if (mat.normalTexture >= 0)
    {
        vec3 normalSample = texture(textures[nonuniformEXT(mat.normalTexture)], payload.texCoord).rgb * 2.0 - 1.0;
        normalSample = normalize(normalSample * vec3(mat.normalScale, mat.normalScale, 1.0));
        
        surface.TBN = GetTBN(payload.normal, payload.tangent);
        surface.TBN = GetTBN(TangentToWorld(normalSample, surface.TBN));
    }

    surface.baseColor = mat.baseColorFactor.rgb;
    if (mat.baseColorTexture >= 0)
    {
        surface.baseColor *= texture(textures[nonuniformEXT(mat.baseColorTexture)], payload.texCoord).rgb;
    }
    surface.baseColor = ToLinear(surface.baseColor);

    surface.roughness = mat.roughnessFactor;
    surface.metallic = mat.metallicFactor;
    if (mat.roughnessMetallicTexture >= 0)
    {
        const vec2 roughnessMetallic = texture(textures[nonuniformEXT(mat.roughnessMetallicTexture)], payload.texCoord).gb;
        surface.roughness *= roughnessMetallic.r;
        surface.metallic *= roughnessMetallic.g;
    }
#if DEBUG_OVERRIDE_MATERIAL
    surface.roughness = DEBUG_ROUGHNESS;
    surface.metallic = DEBUG_METALLIC;
#endif

    surface.emission = mat.emissionFactor.rgb;
    if (mat.emissionTexture >= 0)
    {
        surface.emission *= texture(textures[nonuniformEXT(mat.emissionTexture)], payload.texCoord).rgb;
    }
    surface.emission = ToLinear(surface.emission);

    surface.F0 = mix(DIELECTRIC_F0, surface.baseColor, surface.metallic);
    surface.a  = surface.roughness * surface.roughness;
    surface.a2 = max(surface.a * surface.a, EPSILON);
    surface.sw = GetSpecularWeight(surface.baseColor, surface.F0, surface.metallic);
}

uvec3 GetIndices(uint instanceId, uint primitiveId)
{
    return uvec3(indicesData[nonuniformEXT(instanceId)].indices[primitiveId * 3 + 0],
                 indicesData[nonuniformEXT(instanceId)].indices[primitiveId * 3 + 1],
                 indicesData[nonuniformEXT(instanceId)].indices[primitiveId * 3 + 2]);
}

vec2 GetTexCoord(uint instanceId, uint i)
{
    const VertexRT vertex = verticesData[nonuniformEXT(instanceId)].vertices[i];

    return vec2(vertex.normal.w, vertex.tangent.w);
}

float TraceVisibilityRay(Ray ray)
{
    rayQueryEXT rayQuery;

    const uint rayFlags = gl_RayFlagsTerminateOnFirstHitEXT;

    rayQueryInitializeEXT(rayQuery, tlas, rayFlags, 0xFF,
            ray.origin, ray.TMin, ray.direction, ray.TMax);

    while (rayQueryProceedEXT(rayQuery))
    {
        if (rayQueryGetIntersectionTypeEXT(rayQuery, false) == gl_RayQueryCandidateIntersectionTriangleEXT)
        {
            const uint customIndex = rayQueryGetIntersectionInstanceCustomIndexEXT(rayQuery, false);
            const uint primitiveId = rayQueryGetIntersectionPrimitiveIndexEXT(rayQuery, false);
            const vec2 hitCoord = rayQueryGetIntersectionBarycentricsEXT(rayQuery, false);

            const uint instanceId = customIndex & 0x0000FFFF;
            const uint materialId = customIndex >> 16;

            const uvec3 indices = GetIndices(instanceId, primitiveId);

            const vec2 texCoord0 = GetTexCoord(instanceId, indices[0]);
            const vec2 texCoord1 = GetTexCoord(instanceId, indices[1]);
            const vec2 texCoord2 = GetTexCoord(instanceId, indices[2]);
            
            const vec3 baryCoord = vec3(1.0 - hitCoord.x - hitCoord.y, hitCoord.x, hitCoord.y);

            const vec2 texCoord = BaryLerp(texCoord0, texCoord1, texCoord2, baryCoord);

            const Material mat = materials[materialId];

            float alpha = mat.baseColorFactor.a;
            if (mat.baseColorTexture >= 0)
            {
                alpha *= texture(textures[nonuniformEXT(mat.baseColorTexture)], texCoord).a;
            }

            if (alpha >= mat.alphaCutoff)
            {
                rayQueryConfirmIntersectionEXT(rayQuery);
            }
        }
    }

    if (rayQueryGetIntersectionTypeEXT(rayQuery, true) == gl_RayQueryCommittedIntersectionTriangleEXT)
    {
        return rayQueryGetIntersectionTEXT(rayQuery, true);
    }

    return -1.0;
}

#if LIGHT_COUNT > 0
float EstimateLight(uint index, Surface surface, vec3 p, vec3 wo)
{
    const Light light = lights[index];

    const vec3 direction = light.location.xyz - p * light.location.w;
    
    const float distanceSquare = dot(direction, direction);
    const float attenuation = Select(1.0, Rcp(distanceSquare), light.location.w);

    const vec3 N = surface.TBN[2];
    const vec3 L = normalize(direction);

    const float NoL = max(dot(N, L), 0.0);
    
    const float irradiance = attenuation * NoL * Luminance(light.color.rgb);

    return irradiance;
}

#define UNIFORM_LIGHT_SELECTION 0
int SampleLight(Surface surface, vec3 p, vec3 wo, out float pdf, inout uvec2 seed)
{
#if UNIFORM_LIGHT_SELECTION
    pdf = 1.0 / LIGHT_COUNT;
    return int(NextFloat(seed) * LIGHT_COUNT);
#else
    float irradianceEstimation[LIGHT_COUNT];

    irradianceEstimation[0] = EstimateLight(0, surface, p, wo);
    for (uint i = 1; i < LIGHT_COUNT; ++i)
    {
        irradianceEstimation[i] = EstimateLight(i, surface, p, wo);
        irradianceEstimation[i] += irradianceEstimation[i - 1];
    }

    if (irradianceEstimation[LIGHT_COUNT - 1] > 0.0)
    {
        for (uint i = 0; i < LIGHT_COUNT - 1; ++i)
        {
            irradianceEstimation[i] /= irradianceEstimation[LIGHT_COUNT - 1];
        }
        irradianceEstimation[LIGHT_COUNT - 1] = 1.0;

        const float randSample = NextFloat(seed);

        int lightIndex = 0;
        for (lightIndex = 0; lightIndex < LIGHT_COUNT - 1; ++lightIndex)
        {
            if (randSample < irradianceEstimation[lightIndex])
            {
                break;
            }
        }

        pdf = irradianceEstimation[lightIndex];      
        if (lightIndex > 0)
        {
            pdf -= irradianceEstimation[lightIndex - 1];
        }

        return lightIndex;
    }
    else
    {
        return -1;
    }
#endif
}

vec3 CalculateLightDistortion(vec3 n, float w, inout uvec2 seed)
{
    const vec3 u = normalize(vec3(n.y, -n.x, 0.0));
    const vec3 v = normalize(cross(n, u));

    const float theta = NextFloat(seed) * 2.0 * PI;
    vec3 offset = cos(theta) * u + sin(theta) * v;

    const float r = NextFloat(seed);
    const float d = sqrt(1.0 - r * r);

    offset *= r;
    offset += normalize(n) * d * w;

    return offset;
}

vec3 DirectLighting(Surface surface, vec3 p, vec3 wo, inout uvec2 seed)
{
    float lightPdf;
    const int lightIndex = SampleLight(surface, p, wo, lightPdf, seed);

    if (lightIndex >= 0)
    {
        const Light light = lights[lightIndex];

        vec3 direction = light.location.xyz - p * light.location.w;
        
        const vec3 distortion = CalculateLightDistortion(-direction, light.location.w, seed);
        direction += distortion * Select(DIRECT_LIGHT_DISK_RADIUS, POINT_LIGHT_RADIUS, light.location.w);

        const float distance = Select(RAY_MAX_T, length(direction), light.location.w);
        const float attenuation = Select(1.0, Rcp(Pow2(distance)), light.location.w);

        direction = normalize(direction);

        const vec3 wi = WorldToTangent(direction, surface.TBN);
        const vec3 wh = normalize(wo + wi);

        Ray ray;
        ray.origin = p + surface.TBN[2] * BIAS;
        ray.direction = direction;
        ray.TMin = RAY_MIN_T;
        ray.TMax = distance;

        if (IsMiss(TraceVisibilityRay(ray)))
        {
            const vec3 brdf = EvaluateBRDF(surface, wo, wi, wh);

            return brdf * CosThetaTangent(wi) * light.color.rgb * attenuation / lightPdf;
        }
    }
    
    return vec3(0.0);
}
#endif

#if ACCUMULATION
vec3 AccumulateResult(ivec2 coord, vec3 result)
{
    const vec3 lastResult = imageLoad(accumulationTarget, coord).rgb;
    result = (result + accumIndex * lastResult) / (accumIndex + 1.0);

    imageStore(accumulationTarget, coord, vec4(result, 1.0));

    return result;
}
#endif

void main()
{
    vec3 result = vec3(0.0);
    for (uint sampleIndex = 0; sampleIndex < SAMPLE_COUNT; ++sampleIndex)
    {
        uvec2 seed = GetSeed(gl_LaunchIDEXT.xy, sampleIndex);

        Ray ray;
        ray.origin = GetPrimaryRayOrigin();
        ray.direction = GetPrimaryRayDireciton(seed);
        ray.TMin = camera.zNear;
        ray.TMax = camera.zFar;

        const uint rayFlags = gl_RayFlagsCullBackFacingTrianglesEXT;

        traceRayEXT(tlas, rayFlags, 0xFF, 0, 0, 0,
                ray.origin, ray.TMin, ray.direction, ray.TMax, 0);

        vec3 irradiance = vec3(0.0);

        vec3 rayThroughput = vec3(1.0);
        float rayPdf = 1.0;

        Surface surface;

        for (uint bounceCount = 0; bounceCount < MAX_BOUNCE_COUNT; ++bounceCount)
        {
            if (IsMiss(payload.hitT))
            {
                irradiance += texture(environmentMap, ray.direction).rgb * rayThroughput / rayPdf;
                break;
            }

            UnpackMaterial(materials[payload.matId], surface);

            irradiance += surface.emission * rayThroughput / rayPdf;

            const vec3 p = ray.origin + ray.direction * payload.hitT;
            const vec3 wo = normalize(WorldToTangent(-ray.direction, surface.TBN));

        #if DEBUG_VIEW_DIRECT_LIGHTING && LIGHT_COUNT > 0
            irradiance += DirectLighting(surface, p, wo, seed) * rayThroughput / rayPdf;
        #endif

        #if DEBUG_VIEW_INDIRECT_LIGHTING
            vec3 wi; float pdf;
            const vec3 brdf = SampleBRDF(surface, wo, wi, pdf, seed);

            if (pdf < EPSILON || IsBlack(brdf))
            {
                break;
            }

            const vec3 throughput = brdf * CosThetaTangent(wi);
        
            rayThroughput *= throughput;
            rayPdf *= pdf;

            if (bounceCount >= MIN_BOUNCE_COUNT)
            {
                const float threshold = max(MIN_THRESHOLD, 1.0 - MaxComponent(rayThroughput));
                if (NextFloat(seed) < threshold)
                {
                    break;
                }
                rayThroughput /= 1.0 - threshold;
            }

            ray.origin = p;
            ray.direction = TangentToWorld(wi, surface.TBN);
            ray.TMin = RAY_MIN_T;
            ray.TMax = RAY_MAX_T;

            traceRayEXT(tlas, rayFlags, 0xFF, 0, 0, 0,
                    ray.origin, ray.TMin, ray.direction, ray.TMax, 0);
        #else
            break;
        #endif
        }

        result += min(irradiance, MAX_IRRADIANCE);
    }

    result /= SAMPLE_COUNT;
    
    const ivec2 coord = ivec2(gl_LaunchIDEXT.xy);

#if ACCUMULATION
    result = AccumulateResult(coord, result);
#endif

#if !RENDER_TO_HDR
    result = ToneMapping(result);
#endif

    imageStore(renderTarget, coord, vec4(result, 1.0));
}
